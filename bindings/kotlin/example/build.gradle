plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

Properties localProperties = new Properties()
localProperties.load(rootProject.file('local.properties').newDataInputStream())
def useLocalLib = false
if (localProperties.containsKey("useLocalLib"))
    useLocalLib = localProperties.getProperty("useLocalLib").toBoolean()

android {
    compileSdkVersion 34
    namespace 'org.equalitie.ouisync.example'

    defaultConfig {
        applicationId 'org.equalitie.ouisync.example'
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1
        versionName '0.0.1'
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = '1.5.3'
    }
}

dependencies {
    implementation platform('androidx.compose:compose-bom:2023.09.01')
    implementation 'androidx.compose.material3:material3'
    implementation "androidx.activity:activity-compose:1.7.2"

    if (useLocalLib) {
        implementation project(':lib')
    }
    else {
        implementation 'ie.equalit.ouinet:ouisync-omni:0.7.2'
        implementation 'net.java.dev.jna:jna:5.13.0@aar'
        implementation 'org.msgpack:msgpack-core:0.9.5'
    }
}
