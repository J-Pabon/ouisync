plugins {
    // Apply the org.jetbrains.kotlin.jvm Plugin to add support for Kotlin.
    id 'org.jetbrains.kotlin.jvm' version '1.9.0'

    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'

    // Formatter and linter
    id 'com.diffplug.spotless' version '6.21.0'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    implementation 'net.java.dev.jna:jna:5.13.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    implementation 'org.msgpack:msgpack-core:0.9.5'

    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.3'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

sourceSets {
    main {
        kotlin {
            srcDir layout.buildDirectory.dir('gen')
        }
    }
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

test {
    useJUnitPlatform()

    testLogging {
        showStandardStreams true
        exceptionFormat "full"
    }
}

spotless {
    kotlin {
        ktfmt()
        ktlint()
        // diktat()
    }
}

tasks.register('generateBindings', Exec) {
    description 'Generate bindings'
    workingDir "$rootDir/../.."
    commandLine 'cargo', 'run', '--package', 'ouisync-bindgen', '--', '--language', 'kotlin'

    doFirst {
        def dir = layout.buildDirectory.dir('gen').get().getAsFile()
        mkdir(dir)
        standardOutput = new FileOutputStream("$dir/Bindings.kt")
    }
}

tasks.named('compileKotlin') {
    dependsOn 'generateBindings'
}

tasks.register('buildTestNative', Exec) {
    description 'Build the native library'
    workingDir "$rootDir/../.."
    commandLine 'cargo', 'build', '--package', 'ouisync-ffi', '--lib'
}

tasks.named('test') {
    dependsOn 'buildTestNative'
    systemProperty('jna.library.path', "$rootDir/../../target/debug")
}

